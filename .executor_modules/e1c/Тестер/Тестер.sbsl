#требуется ИзвлекательТестовыхМетодов.sbsl

@Глобально
структура ОписаниеТеста
    обз знч ИмяТеста: Строка
    обз знч Действие: () -> ничто
;

структура КонтекстТестера
    пер Ошибки: Булево = Ложь
;

@Глобально
метод Тестировать(ПутьКСкрипту: Строка, ОбратныйВызовТеста: (Строка) -> ничто)
    знч ФайлСкрипта = новый Файл(ПутьКСкрипту)
    если не ФайлСкрипта.Существует()
        выбросить новый ИсключениеНедопустимоеСостояние("Не найден файл тестируемого модуля `%ПутьКСкрипту`")
    ;

    знч ИмяТестовыхКейсов = ФайлСкрипта.Имя
    знч ТестовыеМетоды = ИзвлекательТестовыхМетодов.ИзвлечьТестовыеМетоды(ФайлСкрипта)
    // Выполнить("%ИмяМетода()"))
    знч СписокТестов = ТестовыеМетоды
        .Преобразовать((ИмяМетода) -> новый Тестер.ОписаниеТеста(ИмяМетода, () -> ОбратныйВызовТеста(ИмяМетода)))

    Тестер.Тестировать(ИмяТестовыхКейсов, СписокТестов)
;

@Глобально
метод Тестировать(ИмяТестовыхКейсов: Строка, СписокТестов: ЧитаемыйМассив<ОписаниеТеста>)
    знч КонтекстТестера = новый КонтекстТестера() 
    
    Консоль.Записать("Тестирование `%ИмяТестовыхКейсов`:")

    СписокТестов.ДляКаждого(ОписаниеТеста -> ВыполнитьТест(КонтекстТестера, ОписаниеТеста))

    если КонтекстТестера.Ошибки
        Консоль.Записать("\нВ ходе тестирования происходили ошибки.")
        Скрипт.ЗавершитьРаботу(1)
    иначе
        Консоль.Записать("\нТестирование завершено успешно.")
    ;
;

метод ВыполнитьТест(ТекущийКонтекст: КонтекстТестера, ОписаниеТеста: ОписаниеТеста)
    попытка
        Консоль.Записать("-> " + ОписаниеТеста.ИмяТеста)
        ОписаниеТеста.Действие()
    поймать Исключение: Исключение
        Консоль.ЗаписатьОшибку("Ошибка: " + Исключение.Описание)
        ТекущийКонтекст.Ошибки = Истина
    ;
;

метод СписокТестов(ПутьКФайлу: Строка): ЧитаемыйМассив<ОписаниеТеста>
    Консоль.Записать(ПутьКФайлу)
    возврат []
;